use Cobra.Compiler

use MonoDevelop.Ide.TypeSystem

use ICSharpCode.NRefactory.TypeSystem

namespace MonoDevelop.CobraBinding.TypeSystem

class ParsedCobraDocument inherits DefaultParsedDocument

	test regionMap
		doc = ParsedCobraDocument("test.cobra", "class Foo\n\tpass")
		assert doc.regionMap == nil
		doc.regionMap = Dictionary<of ISyntaxNode, DomRegion>()
		assert doc.regionMap.count == 0

		expect ArgumentNullException
			doc.regionMap = nil

	test symbolTable
		doc = ParsedCobraDocument("test.cobra", "class Foo\n\tpass")
		assert doc.symbolTable == nil
		doc.symbolTable = Dictionary<of DomRegion, Dictionary<of String, INamedNode>>()
		assert doc.symbolTable.count == 0

		expect ArgumentNullException
			doc.symbolTable = nil

	var _content as String
	var _regionMap as Dictionary<of ISyntaxNode, DomRegion>?
	var _symbolTable as Dictionary<of DomRegion, Dictionary<of String, INamedNode>>?

	cue init(fileName as String, content as String)
		base.init(fileName)
		_content = content

	get content from var

	pro regionMap as Dictionary<of ISyntaxNode, DomRegion>?
		get
			return _regionMap
		set
			if value == nil
				throw ArgumentNullException()
			_regionMap = value

	pro symbolTable as Dictionary<of DomRegion, Dictionary<of String, INamedNode>>?
		get
			return _symbolTable
		set
			if value == nil
				throw ArgumentNullException()
			_symbolTable = value

	def add(err as ParserException)
		.add(Error(ErrorType.Error, err.message, err.lineNum, err.colNum - 1))
		
	def add(err as SourceException)
		col = err.colNum
		# most of the time, we don't get a valid column number for these.
		# but just in case...
		if col > 0, col -= 1
		.add(Error(ErrorType.Error, err.message, err.lineNum, col))